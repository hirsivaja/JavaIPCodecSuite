plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.hirsivaja'
version = '1.1.3'

repositories {
    mavenCentral()
}

sourceSets {
    testi {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    testiImplementation.extendsFrom testImplementation
}

tasks.register('testIntegration', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.testi.output.classesDirs
    classpath = sourceSets.testi.runtimeClasspath
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    withJavadocJar()
    withSourcesJar()
}

test {
    systemProperty 'java.util.logging.config.file', 'src/test/resources/logging.properties'
}

signing {
    def signingKeyId = System.getenv("SIGNING_ID")
    def signingKeyData = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKeyId, signingKeyData, signingPassword)
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = '${project.groupId}:${project.artifactId}'
                description = 'The Java IP Codec Suite contains codecs for various Internet Protocols like IPv4 and RPL'
                url = 'https://github.com/hirsivaja/JavaIPCodecSuite'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'hirsivaja'
                        name = 'Toni Lappalainen'
                        email = 'Toni.Lappalainen@landisgyr.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/hirsivaja/JavaIPCodecSuite.git'
                    developerConnection = 'scm:git:https://github.com/hirsivaja/JavaIPCodecSuite.git'
                    url = 'https://github.com/hirsivaja/JavaIPCodecSuite'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv("CENTRAL_PORTAL_USERNAME")
                password = System.getenv("CENTRAL_PORTAL_PASSWORD")
            }
            name = 'ossrh-staging-api-for-central-portal'
            url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
        }
    }
}
